plugins {
    id 'java'
    id 'maven'
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

//from now on this is how you'll change version
def versionObj = new Version(major: 1, minor: 4, patch: 0, fix: 0, state: 'BETA')

group 'us.eunoians'
version = "${versionObj.toString()}"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://jitpack.io' }
    maven { url 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://nexus.articdive.de/repository/maven-public/' }
    maven { url 'http://maven.sk89q.com/repo/' }
    maven { url 'https://nexus.wesjd.net/repository/thirdparty/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://repo.md-5.net/content/repositories/snapshots/' }
    maven { url 'https://repo.md-5.net/content/repositories/releases/' }
}

dependencies {
    compile 'com.github.cyr1en:javen:1.0.2'
    annotationProcessor 'com.github.cyr1en:javen:1.0.2'

    //compile 'com.github.CyR1en:McUtils:1.6.11'
    compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.26'

    compileOnly 'fr.neatmonster:nocheatplus:3.16.1-SNAPSHOT'
    compileOnly 'com.github.DiamondDagger590:FlatDB:1.0.7'
    compileOnly 'com.github.DiamondDagger590:EnumToYaml:1.0'
    compile 'de.tr7zw:item-nbt-api:2.4.1'
    compile group: 'org.javassist', name: 'javassist', version: '3.21.0-GA'
    compileOnly 'me.clip:placeholderapi:2.9.2'
    compileOnly 'com.sk89q.worldguard:worldguard-core:7.0.0-SNAPSHOT'
    compileOnly 'com.sk89q.worldguard:worldguard-legacy:7.0.0-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT'
    compile 'org.bstats:bstats-bukkit:1.7'
    compileOnly files('libs/Sickle.jar')
    compileOnly files('libs/SpartanAPI.jar')
    compileOnly files('libs/mcMMO.jar')

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'pluginVersion': version
        include 'plugin.yml'
    }
}

shadowJar {
    archiveName = "McRPG-${version}.jar"
    relocate 'de.tr7zw.changeme.nbtapi', 'us.eunoians.mcrpg.nbtapi'
    relocate 'org.slf4j', 'us.eunoians.mcrpg'
    relocate 'org.bstats', 'us.eunoians.mcrpg'
}

artifacts {
    archives shadowJar
}

lombok {
    version = "1.18.4"
    sha256 = ""
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

class Version {

    String major, minor, patch, fix, state, full

    String getFullVersion() {
        full = "$major.$minor.$patch.$fix-$state"
        return full
    }

    String toString() {
        getFullVersion()
    }
}
